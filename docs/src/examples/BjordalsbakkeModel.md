```@meta
EditURL = "<unknown>/BjordalsbakkeModel.jl"
```

````julia
using ModelingToolkit, DifferentialEquations, Plots
using CirculationModels
````

# A simple single-chamber model

![Single chamber, closed-loop, lumped parameter model of the systemic circulation and the left ventricle. The circuit equivalent formulation of the model is depicted, with the pressures of each compartment, as well as most of the mechanical parameters. The model describes three compartments: the left ventricular, arterial and venous compartments. ùëÉùë°‚Ñé is the intrathoracic pressure, ùëÉùëôùë£ is the left ventricular pressure and ùê∏ùëôùë£(ùë°) indicates the left ventricular elastance function.](./BjordalsbakkeModelSketch.png)


This follows Bj√∏rdalsbakke et al.

Bj√∏rdalsbakke, N.L., Sturdy, J.T., Hose, D.R., Hellevik, L.R., 2022. Parameter estimation for closed-loop lumped parameter models of the systemic circulation using synthetic data. Mathematical Biosciences 343, 108731. https://doi.org/10.1016/j.mbs.2021.108731


Changes from the published version above:

- Capacitors are replaced by compliances. These are identical to capacitors, but have an additional parameter, the unstrained volume $V_0$, which allows for realistic blood volume modelling.
  Compliances have an inlet and an oulet in line with the flow, rather than the ground connector of the dangling capacitor.
- The aortic resistor is combined with the valve (diode) in the `ResistorDiode` element.

[Jupyter Notebook](./BjordalsbakkeModel.ipynb)

## Define the parameters

All the parameters are taken from table 1 of [Bj√∏rdalsbakke2022].

Cycle time in seconds

````julia
œÑ = 0.85
````

````
0.85
````

Double Hill parameters for the ventricle

````julia
E‚Çò·µ¢‚Çô = 0.03
E‚Çò‚Çê‚Çì = 1.5
n1LV    = 1.32;
n2LV    = 21.9;
Tau1fLV = 0.303 * œÑ;
Tau2fLV = 0.508 * œÑ
````

````
0.4318
````

Resistances and Compliances

````julia
Rs = 1.11
Csa = 1.13
Csv = 11.0
````

````
11.0
````

Valve parameters

Aortic valve basic

````julia
Zao = 0.033
````

````
0.033
````

Mitral valve basic

````julia
Rmv = 0.006
````

````
0.006
````

Inital Pressure (mean cardiac filling pressure)

````julia
MCFP = 7.0
````

````
7.0
````

## Calculating the additional `k` parameter

The ventricle elastance is modelled as:

$$E_{l v}(t)=\left(E_{\max }-E_{\min }\right) e(t)+E_{\min }$$

where $e$ is a double-Hill function, i.e., two Hill-functions, which are multiplied by each other:

$$e(\tau)= k \times \frac{\left(\tau / \tau_1\right)^{n_1}}{1+\left(\tau / \tau_1\right)^{n_1}} \times \frac{1}{1+\left(\tau / \tau_2\right)^{n_2}}$$

$k$ is a scaling factor to assure that $e(t)$ has a maximum of $e(t)_{max} = 1$:

$$k = \max \left(\frac{\left(\tau / \tau_1\right)^{n_1}}{1+\left(\tau / \tau_1\right)^{n_1}} \times \frac{1}{1+\left(\tau / \tau_2\right)^{n_2}} \right)^{-1}$$ .

````julia
nstep = 1000
t = LinRange(0, œÑ, nstep)

kLV = 1 / maximum((t ./ Tau1fLV).^n1LV ./ (1 .+ (t ./ Tau1fLV).^n1LV) .* 1 ./ (1 .+ (t ./ Tau2fLV).^n2LV))
````

````
1.6721792928965973
````

## Set up the model elements

Set up time as a parameter `t`

````julia
@parameters t
````

````
1-element Vector{Symbolics.Num}:
 t
````

Heart is modelled as a single chamber (we call it `LV` for "Left Ventricle" so the model can be extended later, if required):

````julia
@named LV = DHChamber(V‚ÇÄ = 0.0, E‚Çò‚Çê‚Çì=E‚Çò‚Çê‚Çì, E‚Çò·µ¢‚Çô=E‚Çò·µ¢‚Çô, n‚ÇÅ=n1LV, n‚ÇÇ=n2LV, œÑ = œÑ, œÑ‚ÇÅ=Tau1fLV, œÑ‚ÇÇ=Tau2fLV, k = kLV, Eshift=0.0, Ev=Inf)
````

````
Model LV with 4 (6) equations
States (6):
  V(t) [defaults to 2.0]
  p(t) [defaults to 0.0]
  in‚Çäp(t) [defaults to 1.0]
  in‚Çäq(t) [defaults to 1.0]
  out‚Çäp(t) [defaults to 1.0]
  out‚Çäq(t) [defaults to 1.0]
Parameters (11):
  V‚ÇÄ [defaults to 0.0]
  E‚Çò·µ¢‚Çô [defaults to 0.03]
  E‚Çò‚Çê‚Çì [defaults to 1.5]
  n‚ÇÅ [defaults to 1.32]
  n‚ÇÇ [defaults to 21.9]
  œÑ [defaults to 0.85]
  œÑ‚ÇÅ [defaults to 0.25755]
  œÑ‚ÇÇ [defaults to 0.4318]
  k [defaults to 1.67218]
  Eshift [defaults to 0.0]
  Ev [defaults to Inf]
````

The two valves are simple diodes with a small resistance
(resistance is needed, since perfect diodes would connect two elastances/compliances, which will lead to unstable oscillations):

````julia
@named AV = ResistorDiode(R=Zao)
@named MV = ResistorDiode(R=Rmv)
````

````
Model MV with 4 (6) equations
States (6):
  Œîp(t) [defaults to 0.0]
  q(t) [defaults to 0.0]
  in‚Çäp(t) [defaults to 1.0]
  in‚Çäq(t) [defaults to 1.0]
  out‚Çäp(t) [defaults to 1.0]
  out‚Çäq(t) [defaults to 1.0]
Parameters (1):
  R [defaults to 0.006]
````

The main components of the circuit are 1 resistor `Rs` and two compliances for systemic arteries `Csa`,
and systemic veins `Csv` (names are arbitrary).

````julia
@named Rs = Resistor(R=Rs)

@named Csa = Compliance(C=Csa)
@named Csv = Compliance(C=Csv)
````

````
Model Csv with 4 (6) equations
States (6):
  V(t) [defaults to 0.0]
  p(t) [defaults to 0.0]
  in‚Çäp(t) [defaults to 1.0]
  in‚Çäq(t) [defaults to 1.0]
  out‚Çäp(t) [defaults to 1.0]
  out‚Çäq(t) [defaults to 1.0]
Parameters (2):
  V‚ÇÄ [defaults to 0.0]
  C [defaults to 11.0]
````

We also need to define a base pressure level, which we use the `Ground` element for:

````julia
@named ground = Ground(P=0)
````

````
Model ground with 1 (2) equations
States (2):
  g‚Çäp(t) [defaults to 1.0]
  g‚Çäq(t) [defaults to 1.0]
Parameters (1):
  P [defaults to 0]
````

## Build the system

### Connections

The system is built using the `connect` function. `connect` sets up the Kirchhoff laws:

- pressures are the same in all connected branches on a connector
- sum of all flow rates at a connector is zero

The resulting set of Kirchhoff equations is stored in `circ_eqs`:

````julia
circ_eqs = [
    connect(LV.out, AV.in)
    connect(AV.out, Csa.in)
    connect(Csa.out, Rs.in)
    connect(Rs.out, Csv.in)
    connect(Csv.out, MV.in)
    connect(MV.out, LV.in)
]
````

````
6-element Vector{Symbolics.Equation}:
 connect(LV.out, AV.in)
 connect(AV.out, Csa.in)
 connect(Csa.out, Rs.in)
 connect(Rs.out, Csv.in)
 connect(Csv.out, MV.in)
 connect(MV.out, LV.in)
````

### Add the component equations

In a second step, the system of Kirchhoff equations is completed by the component equations (both ODEs and AEs), resulting in the full, overdefined ODE set `circ_model`.

_Note: we do this in two steps._

````julia
@named _circ_model = ODESystem(circ_eqs, t)

@named circ_model = compose(_circ_model,
                          [LV, AV, MV, Rs, Csa, Csv, ground])
````

````
Model circ_model with 25 (38) equations
States (38):
  LV‚ÇäV(t) [defaults to 2.0]
  LV‚Çäp(t) [defaults to 0.0]
  LV‚Çäin‚Çäp(t) [defaults to 1.0]
  LV‚Çäin‚Çäq(t) [defaults to 1.0]
  LV‚Çäout‚Çäp(t) [defaults to 1.0]
  LV‚Çäout‚Çäq(t) [defaults to 1.0]
  AV‚ÇäŒîp(t) [defaults to 0.0]
  AV‚Çäq(t) [defaults to 0.0]
  AV‚Çäin‚Çäp(t) [defaults to 1.0]
  AV‚Çäin‚Çäq(t) [defaults to 1.0]
  AV‚Çäout‚Çäp(t) [defaults to 1.0]
  AV‚Çäout‚Çäq(t) [defaults to 1.0]
  MV‚ÇäŒîp(t) [defaults to 0.0]
  MV‚Çäq(t) [defaults to 0.0]
  MV‚Çäin‚Çäp(t) [defaults to 1.0]
  MV‚Çäin‚Çäq(t) [defaults to 1.0]
  MV‚Çäout‚Çäp(t) [defaults to 1.0]
  MV‚Çäout‚Çäq(t) [defaults to 1.0]
  Rs‚ÇäŒîp(t) [defaults to 0.0]
  Rs‚Çäq(t) [defaults to 0.0]
  Rs‚Çäin‚Çäp(t) [defaults to 1.0]
  Rs‚Çäin‚Çäq(t) [defaults to 1.0]
  Rs‚Çäout‚Çäp(t) [defaults to 1.0]
  Rs‚Çäout‚Çäq(t) [defaults to 1.0]
  Csa‚ÇäV(t) [defaults to 0.0]
  Csa‚Çäp(t) [defaults to 0.0]
  Csa‚Çäin‚Çäp(t) [defaults to 1.0]
  Csa‚Çäin‚Çäq(t) [defaults to 1.0]
  Csa‚Çäout‚Çäp(t) [defaults to 1.0]
  Csa‚Çäout‚Çäq(t) [defaults to 1.0]
  Csv‚ÇäV(t) [defaults to 0.0]
  Csv‚Çäp(t) [defaults to 0.0]
  Csv‚Çäin‚Çäp(t) [defaults to 1.0]
  Csv‚Çäin‚Çäq(t) [defaults to 1.0]
  Csv‚Çäout‚Çäp(t) [defaults to 1.0]
  Csv‚Çäout‚Çäq(t) [defaults to 1.0]
  ground‚Çäg‚Çäp(t) [defaults to 1.0]
  ground‚Çäg‚Çäq(t) [defaults to 1.0]
Parameters (19):
  LV‚ÇäV‚ÇÄ [defaults to 0.0]
  LV‚ÇäE‚Çò·µ¢‚Çô [defaults to 0.03]
  LV‚ÇäE‚Çò‚Çê‚Çì [defaults to 1.5]
  LV‚Çän‚ÇÅ [defaults to 1.32]
  LV‚Çän‚ÇÇ [defaults to 21.9]
  LV‚ÇäœÑ [defaults to 0.85]
  LV‚ÇäœÑ‚ÇÅ [defaults to 0.25755]
  LV‚ÇäœÑ‚ÇÇ [defaults to 0.4318]
  LV‚Çäk [defaults to 1.67218]
  LV‚ÇäEshift [defaults to 0.0]
  LV‚ÇäEv [defaults to Inf]
  AV‚ÇäR [defaults to 0.033]
  MV‚ÇäR [defaults to 0.006]
  Rs‚ÇäR [defaults to 1.11]
  Csa‚ÇäV‚ÇÄ [defaults to 0.0]
  Csa‚ÇäC [defaults to 1.13]
  Csv‚ÇäV‚ÇÄ [defaults to 0.0]
  Csv‚ÇäC [defaults to 11.0]
  ground‚ÇäP [defaults to 0]
````

### Simplify the ODE system

The crucial step in any acausal modelling is the sympification and reduction of the OD(A)E system to the minimal set of equations. ModelingToolkit.jl does this in the `structural_simplify` function.

````julia
circ_sys = structural_simplify(circ_model)
````

````
Model circ_model with 3 equations
States (3):
  LV‚Çäp(t) [defaults to 0.0]
  Csa‚Çäp(t) [defaults to 0.0]
  Csv‚Çäp(t) [defaults to 0.0]
Parameters (19):
  LV‚ÇäV‚ÇÄ [defaults to 0.0]
  LV‚ÇäE‚Çò·µ¢‚Çô [defaults to 0.03]
  LV‚ÇäE‚Çò‚Çê‚Çì [defaults to 1.5]
  LV‚Çän‚ÇÅ [defaults to 1.32]
  LV‚Çän‚ÇÇ [defaults to 21.9]
  LV‚ÇäœÑ [defaults to 0.85]
  LV‚ÇäœÑ‚ÇÅ [defaults to 0.25755]
  LV‚ÇäœÑ‚ÇÇ [defaults to 0.4318]
  LV‚Çäk [defaults to 1.67218]
  LV‚ÇäEshift [defaults to 0.0]
  LV‚ÇäEv [defaults to Inf]
  AV‚ÇäR [defaults to 0.033]
  MV‚ÇäR [defaults to 0.006]
  Rs‚ÇäR [defaults to 1.11]
  Csa‚ÇäV‚ÇÄ [defaults to 0.0]
  Csa‚ÇäC [defaults to 1.13]
  Csv‚ÇäV‚ÇÄ [defaults to 0.0]
  Csv‚ÇäC [defaults to 11.0]
  ground‚ÇäP [defaults to 0]
Incidence matrix:3√ó6 SparseArrays.SparseMatrixCSC{Symbolics.Num, Int64} with 12 stored entries:
 √ó  √ó  √ó  √ó  ‚ãÖ  ‚ãÖ
 √ó  √ó  √ó  ‚ãÖ  √ó  ‚ãÖ
 √ó  √ó  √ó  ‚ãÖ  ‚ãÖ  √ó
````

`circ_sys` is now the minimal system of equations. In this case it consists of 3 ODEs for the three pressures.

_Note: `structural_simplify` reduces and optimises the ODE system. It is, therefore, not always obvious, which states it will use and which it will drop. We can use the `states` and `observed` function to check this. It is recommended to do this, since small changes can reorder states, observables, and parameters._

States in the system are now:

````julia
states(circ_sys)
````

````
3-element Vector{Any}:
 LV‚Çäp(t)
 Csa‚Çäp(t)
 Csv‚Çäp(t)
````

Observed variables - the system will drop these from the ODE system that is solved, but it keeps all the algebraic equations needed to calculate them in the system object, as well as the `ODEProblem` and solution object - are:

````julia
observed(circ_sys)
````

````
35-element Vector{Symbolics.Equation}:
 LV‚Çäout‚Çäp(t) ~ LV‚Çäp(t)
 LV‚Çäin‚Çäp(t) ~ LV‚Çäp(t)
 AV‚Çäin‚Çäp(t) ~ LV‚Çäp(t)
 MV‚Çäout‚Çäp(t) ~ LV‚Çäp(t)
 AV‚Çäout‚Çäp(t) ~ Csa‚Çäp(t)
 Rs‚Çäin‚Çäp(t) ~ Csa‚Çäp(t)
 Csa‚Çäout‚Çäp(t) ~ Csa‚Çäp(t)
 Csa‚Çäin‚Çäp(t) ~ Csa‚Çäp(t)
 MV‚Çäin‚Çäp(t) ~ Csv‚Çäp(t)
 Rs‚Çäout‚Çäp(t) ~ Csv‚Çäp(t)
 Csv‚Çäout‚Çäp(t) ~ Csv‚Çäp(t)
 Csv‚Çäin‚Çäp(t) ~ Csv‚Çäp(t)
 ground‚Çäg‚Çäq(t) ~ 0
 ground‚Çäg‚Çäp(t) ~ ground‚ÇäP
 Csv‚ÇäV(t) ~ Csv‚ÇäV‚ÇÄ + Csv‚ÇäC*Csv‚Çäp(t)
 Csa‚ÇäV(t) ~ Csa‚ÇäV‚ÇÄ + Csa‚ÇäC*Csa‚Çäp(t)
 Rs‚ÇäŒîp(t) ~ Csv‚Çäp(t) - Csa‚Çäp(t)
 MV‚ÇäŒîp(t) ~ LV‚Çäp(t) - Csv‚Çäp(t)
 AV‚ÇäŒîp(t) ~ Csa‚Çäp(t) - LV‚Çäp(t)
 LV‚ÇäV(t) ~ LV‚ÇäV‚ÇÄ + LV‚Çäp(t) / (LV‚ÇäE‚Çò·µ¢‚Çô + (LV‚Çäk*(LV‚ÇäE‚Çò‚Çê‚Çì - LV‚ÇäE‚Çò·µ¢‚Çô)*((rem(t + LV‚ÇäœÑ*(1 - LV‚ÇäEshift), LV‚ÇäœÑ) / LV‚ÇäœÑ‚ÇÅ)^LV‚Çän‚ÇÅ)) / ((1 + (rem(t + LV‚ÇäœÑ*(1 - LV‚ÇäEshift), LV‚ÇäœÑ) / LV‚ÇäœÑ‚ÇÅ)^LV‚Çän‚ÇÅ)*(1 + (rem(t + LV‚ÇäœÑ*(1 - LV‚ÇäEshift), LV‚ÇäœÑ) / LV‚ÇäœÑ‚ÇÇ)^LV‚Çän‚ÇÇ)))
 Csv‚Çäin‚Çäq(t) ~ (-Rs‚ÇäŒîp(t)) / Rs‚ÇäR
 Csv‚Çäout‚Çäq(t) ~ (MV‚ÇäŒîp(t)*(MV‚ÇäŒîp(t) < 0)) / MV‚ÇäR
 Csa‚Çäin‚Çäq(t) ~ -((AV‚ÇäŒîp(t)*(AV‚ÇäŒîp(t) < 0)) / AV‚ÇäR)
 Csa‚Çäout‚Çäq(t) ~ -Csv‚Çäin‚Çäq(t)
 Rs‚Çäq(t) ~ Csv‚Çäin‚Çäq(t)
 Rs‚Çäout‚Çäq(t) ~ -Csv‚Çäin‚Çäq(t)
 Rs‚Çäin‚Çäq(t) ~ Csv‚Çäin‚Çäq(t)
 LV‚Çäin‚Çäq(t) ~ -Csv‚Çäout‚Çäq(t)
 MV‚Çäq(t) ~ -Csv‚Çäout‚Çäq(t)
 MV‚Çäout‚Çäq(t) ~ Csv‚Çäout‚Çäq(t)
 MV‚Çäin‚Çäq(t) ~ -Csv‚Çäout‚Çäq(t)
 AV‚Çäq(t) ~ Csa‚Çäin‚Çäq(t)
 LV‚Çäout‚Çäq(t) ~ -Csa‚Çäin‚Çäq(t)
 AV‚Çäout‚Çäq(t) ~ -Csa‚Çäin‚Çäq(t)
 AV‚Çäin‚Çäq(t) ~ Csa‚Çäin‚Çäq(t)
````

And the parameters (these could be reordered, so check these, too):

````julia
parameters(circ_sys)
````

````
19-element Vector{SymbolicUtils.Sym{Real, Base.ImmutableDict{DataType, Any}}}:
 LV‚ÇäV‚ÇÄ
 LV‚ÇäE‚Çò·µ¢‚Çô
 LV‚ÇäE‚Çò‚Çê‚Çì
 LV‚Çän‚ÇÅ
 LV‚Çän‚ÇÇ
 LV‚ÇäœÑ
 LV‚ÇäœÑ‚ÇÅ
 LV‚ÇäœÑ‚ÇÇ
 LV‚Çäk
 LV‚ÇäEshift
 LV‚ÇäEv
 AV‚ÇäR
 MV‚ÇäR
 Rs‚ÇäR
 Csa‚ÇäV‚ÇÄ
 Csa‚ÇäC
 Csv‚ÇäV‚ÇÄ
 Csv‚ÇäC
 ground‚ÇäP
````

### Define the ODE problem

First defined initial conditions `u0` and the time span for simulation:

````julia
u0 = [MCFP, MCFP, MCFP]

tspan = (0, 20)
````

````
(0, 20)
````

in this case we use the mean cardiac filling pressure as initial condition, and simulate 20 seconds.

Then we can define the problem:

````julia
prob = ODEProblem(circ_sys, u0, tspan)
````

````
ODEProblem with uType Vector{Float64} and tType Int64. In-place: true
timespan: (0, 20)
u0: 3-element Vector{Float64}:
 7.0
 7.0
 7.0
````

## Simulate

The ODE problem is now in the MTK/DifferentialEquations.jl format and we can use any DifferentialEquations.jl solver to solve it:

````julia
sol = solve(prob, Vern7(), reltol=1e-12, abstol=1e-12);
````

## Results

````julia
using Plots
using DisplayAs

p1 = plot(sol, idxs=[LV.p,  Csa.in.p], tspan=(16 * œÑ, 17 * œÑ), xlabel = "Time [s]", ylabel = "Pressure [mmHg]",  hidexaxis = nothing) # Make a line plot
p2 = plot(sol, idxs=[LV.V], tspan=(16 * œÑ, 17 * œÑ),xlabel = "Time [s]", ylabel = "Volume [ml]",  linkaxes = :all)
p3 = plot(sol, idxs=[Csa.in.q,Csv.in.q], tspan=(16 * œÑ, 17 * œÑ),xlabel = "Time [s]", ylabel = "Flow rate [ml/s]", linkaxes = :all)

img = plot(p1, p2, p3, layout=@layout([a; b c]), legend = true)

img = DisplayAs.Text(DisplayAs.PNG(img))

img
````
![](BjordalsbakkeModel-48.png)

---

*This page was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*

